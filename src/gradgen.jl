import LinearAlgebra
import QuantumPropagators
import Base: -, *

using QuantumPropagators.Controls: getcontrols, getcontrolderivs


@doc raw"""Extended generator for the standard dynamic gradient.

```julia
GÃÉ = TimeDependentGradGenerator(G)
```

contains the original time-dependent generator `G` (a Hamiltonian or
Liouvillian) in `GÃÉ.G`, a vector of control derivatives ``‚àÇG/‚àÇœµ‚Çó(t)`` in
`GÃÉ.control_derivs`, and the controls in `GÃÉ.controls`.

For a generator ``G = HÃÇ(t) = HÃÇ‚ÇÄ + œµ‚ÇÅ(t) HÃÇ‚ÇÅ + ‚Ä¶ +  œµ‚Çô(t) HÃÇ‚Çô``, this extended
generator encodes the block-matrix

```math
GÃÉ = \begin{pmatrix}
         HÃÇ(t)  &  0    &  \dots   &  0     &  HÃÇ‚ÇÅ     \\
         0     &  HÃÇ(t) &  \dots   &  0     &  HÃÇ‚ÇÇ     \\
    \vdots     &       &  \ddots  &        &  \vdots \\
         0     &  0    &  \dots   &  HÃÇ(t)  &  HÃÇ‚Çô     \\
         0     &  0    &  \dots   &  0     &  HÃÇ(t)
\end{pmatrix}
```

Note that the ``‚àÇG/‚àÇœµ‚Çó(t)`` (``HÃÇ‚Çó`` in the above example) are functions, to
account for the possibility of non-linear control terms, see
[`getcontrolderiv`](@ref QuantumPropagators.Controls.getcontrolderiv).
"""
struct TimeDependentGradGenerator{GT,CT}
    G::GT
    control_derivs::Vector{Function}
    controls::Vector{CT}

    function TimeDependentGradGenerator(G::GT) where {GT}
        controls = collect(getcontrols(G))
        control_derivs = getcontrolderivs(G, controls)
        new{GT,eltype(controls)}(G, control_derivs, controls)
    end

end


"""Static generator for the standard dynamic gradient.

```julia
G::GradGenerator = evalcontrols(G_of_t::TimeDependentGradGenerator, vals_dict)
```

is the result of plugging in specific values for all controls in a
[`TimeDependentGradGenerator`](@ref). See [`evalcontrols`](@ref
QuantumPropagators.Controls.evalcontrols) and [`evalcontrols!`](@ref
QuantumPropagators.Controls.evalcontrols!).

The resulting object can be multiplied directly with a [`GradVector`](@ref),
e.g., in the process of evaluating a piecewise-constant time propagation.
"""
struct GradGenerator{num_controls,GT,CGT}
    G::GT
    control_derivs::Vector{CGT}
end

# Dummy initializer: this creates a GradGenerator that fits a
# TimeDependentGradGenerator structurally
function GradGenerator(G_of_t::TimeDependentGradGenerator)
    dummy_vals = IdDict(control => 1.0 for control in G_of_t.controls)
    dummy_tlist = [0.0, 1.0]
    G = QuantumPropagators.Controls.evalcontrols(G_of_t.G, dummy_vals, dummy_tlist, 1)
    control_derivs = [Œº(1.0, dummy_tlist, 1) for Œº in G_of_t.control_derivs]
    num_controls = length(control_derivs)
    GradGenerator{num_controls,typeof(G),eltype(control_derivs)}(G, control_derivs)
end


function QuantumPropagators.Controls.getcontrols(G_of_t::TimeDependentGradGenerator)
    return getcontrols(G_of_t.G)
end


function QuantumPropagators.Controls.evalcontrols!(
    G::GradGenerator,
    G_of_t::TimeDependentGradGenerator,
    vals_dict::AbstractDict,
    args...
)
    QuantumPropagators.Controls.evalcontrols!(G.G, G_of_t.G, vals_dict, args...)
    for (i, control) in enumerate(G_of_t.controls)
        Œº = G_of_t.control_derivs[i]
        G.control_derivs[i] = Œº(vals_dict[control], args...)
        # In most cases (for linear controls), the above line will be a no-op.
        # Hence, we're not using `copyto!`.
    end
    return G
end


function QuantumPropagators.Controls.evalcontrols(
    G_of_t::TimeDependentGradGenerator,
    vals_dict::AbstractDict,
    args...
)
    G = GradGenerator(G_of_t)
    QuantumPropagators.Controls.evalcontrols!(G, G_of_t, vals_dict, args...)
end


@doc raw"""Extended state-vector for the dynamic gradient.

```julia
Œ®ÃÉ = GradVector(Œ®, num_controls)
```

for an initial state `Œ®` and `num_controls` control fields.

The `GradVector` conceptually corresponds to a direct-sum (block) column-vector
``Œ®ÃÉ = (|Œ®ÃÉ‚ÇÅ‚ü©, |Œ®ÃÉ‚ÇÇ‚ü©, ‚Ä¶ |Œ®ÃÉ‚Çô‚ü©, |Œ®‚ü©)^T``, where ``n`` is `num_controls`. With a
matching ``GÃÉ`` as in the documentation of [`TimeDependentGradGenerator`](@ref),
we have

```math
GÃÉ Œ®ÃÉ = \begin{pmatrix}
HÃÇ |Œ®ÃÉ‚ÇÅ‚ü© + HÃÇ‚ÇÅ|Œ®‚ü© \\
\vdots \\
HÃÇ |Œ®ÃÉ‚Çô‚ü© + HÃÇ‚Çô|Œ®‚ü© \\
HÃÇ |Œ®‚ü©
\end{pmatrix}
```

and

```math
e^{-i GÃÉ dt} \begin{pmatrix} 0 \\ \vdots \\ 0 \\ |Œ®‚ü© \end{pmatrix}
= \begin{pmatrix}
\frac{‚àÇ}{‚àÇœµ‚ÇÅ} e^{-i HÃÇ dt} |Œ®‚ü© \\
\vdots \\
\frac{‚àÇ}{‚àÇœµ‚Çô} e^{-i HÃÇ dt} |Œ®‚ü© \\
e^{-i HÃÇ dt} |Œ®‚ü©
\end{pmatrix}.
```
"""
struct GradVector{num_controls,T}
    state::T
    grad_states::Vector{T}
end

function GradVector(Œ®::T, num_controls::Int64) where {T}
    grad_states = [similar(Œ®) for _ = 1:num_controls]
    for i = 1:num_controls
        fill!(grad_states[i], 0.0)
    end
    GradVector{num_controls,T}(copy(Œ®), grad_states)
end


"""Reset the given gradient vector for a new gradient evaluation.

```julia
resetgradvec!(Œ®ÃÉ::GradVector)
```

zeroes out `Œ®ÃÉ.grad_states` but leaves `Œ®ÃÉ.state` unaffected.

```julia
resetgradvec!(Œ®ÃÉ::GradVector, Œ®)
```

additionally sets `Œ®ÃÉ.state` to `Œ®`.
"""
function resetgradvec!(Œ®ÃÉ::GradVector)
    for i = 1:length(Œ®ÃÉ.grad_states)
        fill!(Œ®ÃÉ.grad_states[i], 0.0)
    end
end

function resetgradvec!(Œ®ÃÉ::GradVector{num_controls,T}, Œ®::T) where {num_controls,T}
    copyto!(Œ®ÃÉ.state, Œ®)
    resetgradvec!(Œ®ÃÉ)
end


function LinearAlgebra.mul!(Œ¶::GradVector, G::GradGenerator, Œ®::GradVector)
    LinearAlgebra.mul!(Œ¶.state, G.G, Œ®.state)
    for i = 1:length(Œ®.grad_states)
        LinearAlgebra.mul!(Œ¶.grad_states[i], G.G, Œ®.grad_states[i])
        LinearAlgebra.mul!(Œ¶.grad_states[i], G.control_derivs[i], Œ®.state, 1, 1)
    end
end


function LinearAlgebra.lmul!(c, Œ®::GradVector)
    LinearAlgebra.lmul!(c, Œ®.state)
    for i ‚àà eachindex(Œ®.grad_states)
        LinearAlgebra.lmul!(c, Œ®.grad_states[i])
    end
end

function LinearAlgebra.axpy!(a, X::GradVector, Y::GradVector)
    LinearAlgebra.axpy!(a, X.state, Y.state)
    for i ‚àà eachindex(X.grad_states)
        LinearAlgebra.axpy!(a, X.grad_states[i], Y.grad_states[i])
    end
end


function LinearAlgebra.norm(Œ®::GradVector)
    nrm = LinearAlgebra.norm(Œ®.state)
    for i ‚àà eachindex(Œ®.grad_states)
        nrm += LinearAlgebra.norm(Œ®.grad_states[i])
    end
    return nrm
end


function LinearAlgebra.dot(Œ®::GradVector, Œ¶::GradVector)
    c::ComplexF64 = LinearAlgebra.dot(Œ®.state, Œ¶.state)
    for i ‚àà eachindex(Œ®.grad_states)
        c += LinearAlgebra.dot(Œ®.grad_states[i], Œ¶.grad_states[i])
    end
    return c
end


LinearAlgebra.ishermitian(G::GradGenerator) = false


# Upper triangular block matrices have eigenvalues only from the diagonal
# blocks. This is an example for a matrix that has real eigenvalues despite not
# being Hermitian
QuantumPropagators.has_real_eigvals(G::GradGenerator) =
    QuantumPropagators.has_real_eigvals(G.G)


function Base.isreal(G::GradGenerator)
    return (isreal(G.G) && all(isreal(D for D in G.control_derivs)))
end


function Base.copyto!(dest::GradVector, src::GradVector)
    copyto!(dest.state, src.state)
    for i = 1:length(src.grad_states)
        copyto!(dest.grad_states[i], src.grad_states[i])
    end
    return dest
end


function Base.copy(Œ®::GradVector)
    Œ¶ = GradVector(Œ®.state, length(Œ®.grad_states))
    for i = 1:length(Œ®.grad_states)
        copyto!(Œ¶.grad_states[i], Œ®.grad_states[i])
    end
    return Œ¶
end


function Base.length(Œ®::GradVector)
    return length(Œ®.state) * (1 + length(Œ®.grad_states))
end


function Base.size(G::GradGenerator)
    return Base.size(G.G)
end


function Base.similar(Œ®::GradVector{num_controls,T}) where {num_controls,T}
    return GradVector{num_controls,T}(similar(Œ®.state), [similar(œï) for œï ‚àà Œ®.grad_states])
end

function Base.similar(G::GradGenerator{num_controls,GT,CGT}) where {num_controls,GT,CGT}
    return GradGenerator{num_controls,GT,CGT}(similar(G.G), similar(G.control_derivs))
end


function Base.copyto!(dest::GradGenerator, src::GradGenerator)
    copyto!(dest.G, src.G)
    copyto!(dest.control_derivs, src.control_derivs)
end


function Base.fill!(Œ®::GradVector, v)
    Base.fill!(Œ®.state, v)
    for i = 1:length(Œ®.grad_states)
        Base.fill!(Œ®.grad_states[i], v)
    end
end


function -(Œ®::GradVector, Œ¶::GradVector)
    res = copy(Œ®)
    LinearAlgebra.axpy!(-1, Œ¶.state, res.state)
    for i = 1:length(Œ®.grad_states)
        LinearAlgebra.axpy!(-1, Œ¶.grad_states[i], res.grad_states[i])
    end
    return res
end


function *(G::GradGenerator{num_controls,GT,CGT}, Œ±::Number) where {num_controls,GT,CGT}
    GradGenerator{num_controls,GT,CGT}(G.G * Œ±, [CG * Œ± for CG in G.control_derivs])
end

*(Œ±::Number, G::GradGenerator) = *(G::GradGenerator, Œ±::Number)


function QuantumPropagators.SpectralRange.random_state(H::GradGenerator)
    state = QuantumPropagators.SpectralRange.random_state(H.G)
    num_controls = length(H.control_derivs)
    grad_states = [
        QuantumPropagators.SpectralRange.random_state(H.G) for
        i ‚àà eachindex(H.control_derivs)
    ]
    return GradVector{num_controls,typeof(state)}(state, grad_states)
end


@inline function convert_gradgen_to_dense(G)
    N = size(G.G)[1]
    L = length(G.control_derivs)
    G_full = zeros(eltype(G.G), N * (L + 1), N * (L + 1))
    convert_gradgen_to_dense!(G_full, G)
end


@inline function convert_gradgen_to_dense!(G_full, G)
    N = size(G.G)[1]
    L = length(G.control_derivs)
    @inbounds for i = 1:L+1
        G_full[(i-1)*N+1:i*N, (i-1)*N+1:i*N] .= G.G
    end
    # Set the control-derivatives in the last (block-)column
    @inbounds for i = 1:L
        G_full[(i-1)*N+1:i*N, L*N+1:(L+1)*N] .= G.control_derivs[i]
    end
    return G_full
end


@inline function convert_gradvec_to_dense(Œ®)
    N = length(Œ®.state)
    L = length(Œ®.grad_states)
    Œ®_full = zeros(ComplexF64, N * (L + 1))
    convert_gradvec_to_dense!(Œ®_full, Œ®)
end


@inline function convert_gradvec_to_dense!(Œ®_full, Œ®)
    N = length(Œ®.state)
    L = length(Œ®.grad_states)
    @inbounds for i = 1:L
        Œ®_full[(i-1)*N+1:i*N] .= Œ®.grad_states[i]
    end
    @inbounds Œ®_full[L*N+1:(L+1)*N] .= Œ®.state
    return Œ®_full
end


@inline function convert_dense_to_gradvec!(Œ®, Œ®_full)
    N = length(Œ®.state)
    L = length(Œ®.grad_states)
    @inbounds for i = 1:L
        Œ®.grad_states[i] .= Œ®_full[(i-1)*N+1:i*N]
    end
    @inbounds Œ®.state .= Œ®_full[L*N+1:(L+1)*N]
    return Œ®
end

function Base.convert(::Type{Vector{ComplexF64}}, gradvec::GradVector)
    convert_gradvec_to_dense(gradvec)
end

function Base.convert(::Type{GradVector{num_controls,T}}, vec::T) where {num_controls,T}
    L = num_controls
    N = length(vec) √∑ (L + 1)  # dimension of state
    @assert length(vec) == (L + 1) * N
    grad_states = [vec[(i-1)*N+1:i*N] for i = 1:L]
    state = vec[L*N+1:(L+1)*N]
    return GradVector{num_controls,T}(state, grad_states)
end


function Base.Array(G::GradGenerator)
    N, M = size(G.G)
    L = length(G.control_derivs)
    ùüò = zeros(eltype(G.G), N, M)
    Œº = G.control_derivs
    block_rows = [hcat([ùüò for j = 1:i-1]..., G.G, [ùüò for j = i+1:L]..., Œº[i]) for i = 1:L]
    last_block_row = hcat([ùüò for j = 1:L]..., G.G)
    return Base.Array(vcat(block_rows..., last_block_row))
end

function Base.convert(::Type{MT}, G::GradGenerator) where {MT<:Matrix}
    Base.convert(MT, Base.Array(G))
end

QuantumPropagators._exp_prop_convert_state(::GradVector) = Vector{ComplexF64}
QuantumPropagators._exp_prop_convert_operator(::GradGenerator) = Matrix{ComplexF64}
QuantumPropagators._exp_prop_convert_operator(::TimeDependentGradGenerator) =
    Matrix{ComplexF64}
