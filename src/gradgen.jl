import LinearAlgebra
import QuantumPropagators
import Base: -, *

using QuantumPropagators.Generators:
    getcontrols, getcontrolderivs, evalcontrols, evalcontrols!


@doc raw"""Extended generator for the standard dynamic gradient.

```julia
GÌƒ = GradGenerator(G)
```

contains the original time-dependent generator `G` (a Hamiltonian or
Liouvillian) in `GÌƒ.G`, a vector of control derivatives ``âˆ‚G/âˆ‚Ïµâ‚—(t)`` in
`GÌƒ.control_derivs`, and the controls in `GÌƒ.controls`.

For a generator ``G = HÌ‚(t) = HÌ‚â‚€ + Ïµâ‚(t) HÌ‚â‚ + â€¦ +  Ïµâ‚™(t) HÌ‚â‚™``, this extended
generator encodes the block-matrix

```math
GÌƒ = \begin{pmatrix}
         HÌ‚(t)  &  0    &  \dots   &  0     &  HÌ‚â‚     \\
         0     &  HÌ‚(t) &  \dots   &  0     &  HÌ‚â‚‚     \\
    \vdots     &       &  \ddots  &        &  \vdots \\
         0     &  0    &  \dots   &  HÌ‚(t)  &  HÌ‚â‚™     \\
         0     &  0    &  \dots   &  0     &  HÌ‚(t)
\end{pmatrix}
```

Note that the ``âˆ‚G/âˆ‚Ïµâ‚—(t)`` (``HÌ‚â‚—`` in the above example) may be
time-dependent, to account for the possibility of non-linear control terms, see
[`getcontrolderiv`](@ref QuantumPropagators.Generators.getcontrolderiv).
"""
struct GradGenerator{GT,CDT,CT}
    G::GT
    control_derivs::Vector{CDT}
    controls::Vector{CT}

    function GradGenerator(G::GT) where {GT}
        controls = collect(getcontrols(G))
        control_derivs = getcontrolderivs(G, controls)
        CT = eltype(controls)
        CDT = eltype(control_derivs)
        new{GT,CDT,CT}(G, control_derivs, controls)
    end

end


"""Static generator for the dynamic gradient.

```julia
G::GradgenOperator = evalcontrols(gradgen::GradGenerator, vals_dict)
```

is the result of plugging in specific values for all controls in a
[`GradGenerator`](@ref). See [`evalcontrols`](@ref
QuantumPropagators.Generators.evalcontrols) and [`evalcontrols!`](@ref
QuantumPropagators.Generators.evalcontrols!).

The resulting object can be multiplied directly with a [`GradVector`](@ref),
e.g., in the process of evaluating a piecewise-constant time propagation.
"""
struct GradgenOperator{num_controls,GT,CGT}
    G::GT
    control_deriv_ops::Vector{CGT}
end

# Dummy initializer: this creates a GradgenOperator that fits a
# GradGenerator structurally
function GradgenOperator(gradgen::GradGenerator)
    dummy_vals = IdDict(control => 1.0 for control in gradgen.controls)
    dummy_tlist = [0.0, 1.0]
    G = evalcontrols(gradgen.G, dummy_vals, dummy_tlist, 1)
    control_deriv_ops =
        [evalcontrols(Î¼, dummy_vals, dummy_tlist, 1) for Î¼ in gradgen.control_derivs]
    num_controls = length(control_deriv_ops)
    GT = typeof(G)
    CGT = eltype(control_deriv_ops)
    GradgenOperator{num_controls,GT,CGT}(G, control_deriv_ops)
end


function QuantumPropagators.Generators.getcontrols(gradgen::GradGenerator)
    return getcontrols(gradgen.G)
end

QuantumPropagators.Generators.evalcontrols(O::GradgenOperator, _...) = O
QuantumPropagators.Generators.evalcontrols!(O1::T, O2::T, _...) where {T<:GradgenOperator} =
    O1
QuantumPropagators.Generators.getcontrols(O1::GradgenOperator) = Tuple([])


function QuantumPropagators.Generators.evalcontrols!(
    G::GradgenOperator,
    gradgen::GradGenerator,
    vals_dict::AbstractDict,
    args...
)
    evalcontrols!(G.G, gradgen.G, vals_dict, args...)
    for (i, control) in enumerate(gradgen.controls)
        Î¼ = gradgen.control_derivs[i]
        G.control_deriv_ops[i] = evalcontrols(Î¼, vals_dict, args...)
        # In most cases (for linear controls), evalcontrols(Î¼, ...) = Î¼
        # Hence, we're not using `copyto!`.
    end
    return G
end


function QuantumPropagators.Generators.evalcontrols(
    gradgen::GradGenerator,
    vals_dict::AbstractDict,
    args...
)
    G = GradgenOperator(gradgen)
    QuantumPropagators.Generators.evalcontrols!(G, gradgen, vals_dict, args...)
end


@doc raw"""Extended state-vector for the dynamic gradient.

```julia
Î¨Ìƒ = GradVector(Î¨, num_controls)
```

for an initial state `Î¨` and `num_controls` control fields.

The `GradVector` conceptually corresponds to a direct-sum (block) column-vector
``Î¨Ìƒ = (|Î¨Ìƒâ‚âŸ©, |Î¨Ìƒâ‚‚âŸ©, â€¦ |Î¨Ìƒâ‚™âŸ©, |Î¨âŸ©)^T``, where ``n`` is `num_controls`. With a
matching ``GÌƒ`` as in the documentation of [`GradGenerator`](@ref),
we have

```math
GÌƒ Î¨Ìƒ = \begin{pmatrix}
HÌ‚ |Î¨Ìƒâ‚âŸ© + HÌ‚â‚|Î¨âŸ© \\
\vdots \\
HÌ‚ |Î¨Ìƒâ‚™âŸ© + HÌ‚â‚™|Î¨âŸ© \\
HÌ‚ |Î¨âŸ©
\end{pmatrix}
```

and

```math
e^{-i GÌƒ dt} \begin{pmatrix} 0 \\ \vdots \\ 0 \\ |Î¨âŸ© \end{pmatrix}
= \begin{pmatrix}
\frac{âˆ‚}{âˆ‚Ïµâ‚} e^{-i HÌ‚ dt} |Î¨âŸ© \\
\vdots \\
\frac{âˆ‚}{âˆ‚Ïµâ‚™} e^{-i HÌ‚ dt} |Î¨âŸ© \\
e^{-i HÌ‚ dt} |Î¨âŸ©
\end{pmatrix}.
```
"""
struct GradVector{num_controls,T}
    state::T
    grad_states::Vector{T}
end

function GradVector(Î¨::T, num_controls::Int64) where {T}
    grad_states = [similar(Î¨) for _ = 1:num_controls]
    for i = 1:num_controls
        fill!(grad_states[i], 0.0)
    end
    GradVector{num_controls,T}(copy(Î¨), grad_states)
end


"""Reset the given gradient vector for a new gradient evaluation.

```julia
resetgradvec!(Î¨Ìƒ::GradVector)
```

zeroes out `Î¨Ìƒ.grad_states` but leaves `Î¨Ìƒ.state` unaffected.

```julia
resetgradvec!(Î¨Ìƒ::GradVector, Î¨)
```

additionally sets `Î¨Ìƒ.state` to `Î¨`.
"""
function resetgradvec!(Î¨Ìƒ::GradVector)
    for i = 1:length(Î¨Ìƒ.grad_states)
        fill!(Î¨Ìƒ.grad_states[i], 0.0)
    end
end

function resetgradvec!(Î¨Ìƒ::GradVector{num_controls,T}, Î¨::T) where {num_controls,T}
    copyto!(Î¨Ìƒ.state, Î¨)
    resetgradvec!(Î¨Ìƒ)
end


function LinearAlgebra.mul!(Î¦::GradVector, G::GradgenOperator, Î¨::GradVector)
    LinearAlgebra.mul!(Î¦.state, G.G, Î¨.state)
    for i = 1:length(Î¨.grad_states)
        LinearAlgebra.mul!(Î¦.grad_states[i], G.G, Î¨.grad_states[i])
        LinearAlgebra.mul!(Î¦.grad_states[i], G.control_deriv_ops[i], Î¨.state, 1, 1)
    end
end


function LinearAlgebra.lmul!(c, Î¨::GradVector)
    LinearAlgebra.lmul!(c, Î¨.state)
    for i âˆˆ eachindex(Î¨.grad_states)
        LinearAlgebra.lmul!(c, Î¨.grad_states[i])
    end
end

function LinearAlgebra.axpy!(a, X::GradVector, Y::GradVector)
    LinearAlgebra.axpy!(a, X.state, Y.state)
    for i âˆˆ eachindex(X.grad_states)
        LinearAlgebra.axpy!(a, X.grad_states[i], Y.grad_states[i])
    end
end


function LinearAlgebra.norm(Î¨::GradVector)
    nrm = LinearAlgebra.norm(Î¨.state)
    for i âˆˆ eachindex(Î¨.grad_states)
        nrm += LinearAlgebra.norm(Î¨.grad_states[i])
    end
    return nrm
end


function LinearAlgebra.dot(Î¨::GradVector, Î¦::GradVector)
    c::ComplexF64 = LinearAlgebra.dot(Î¨.state, Î¦.state)
    for i âˆˆ eachindex(Î¨.grad_states)
        c += LinearAlgebra.dot(Î¨.grad_states[i], Î¦.grad_states[i])
    end
    return c
end


LinearAlgebra.ishermitian(G::GradgenOperator) = false


# Upper triangular block matrices have eigenvalues only from the diagonal
# blocks. This is an example for a matrix that has real eigenvalues despite not
# being Hermitian
QuantumPropagators.has_real_eigvals(G::GradgenOperator) =
    QuantumPropagators.has_real_eigvals(G.G)


function Base.isreal(G::GradgenOperator)
    return (isreal(G.G) && all(isreal(D for D in G.control_deriv_ops)))
end


function Base.copyto!(dest::GradVector, src::GradVector)
    copyto!(dest.state, src.state)
    for i = 1:length(src.grad_states)
        copyto!(dest.grad_states[i], src.grad_states[i])
    end
    return dest
end


function Base.copy(Î¨::GradVector)
    Î¦ = GradVector(Î¨.state, length(Î¨.grad_states))
    for i = 1:length(Î¨.grad_states)
        copyto!(Î¦.grad_states[i], Î¨.grad_states[i])
    end
    return Î¦
end


function Base.length(Î¨::GradVector)
    return length(Î¨.state) * (1 + length(Î¨.grad_states))
end


function Base.size(G::GradgenOperator)
    return Base.size(G.G)
end


function Base.similar(Î¨::GradVector{num_controls,T}) where {num_controls,T}
    return GradVector{num_controls,T}(similar(Î¨.state), [similar(Ï•) for Ï• âˆˆ Î¨.grad_states])
end

function Base.similar(G::GradgenOperator{num_controls,GT,CGT}) where {num_controls,GT,CGT}
    return GradgenOperator{num_controls,GT,CGT}(similar(G.G), similar(G.control_deriv_ops))
end


function Base.copyto!(dest::GradgenOperator, src::GradgenOperator)
    copyto!(dest.G, src.G)
    copyto!(dest.control_deriv_ops, src.control_deriv_ops)
end


function Base.fill!(Î¨::GradVector, v)
    Base.fill!(Î¨.state, v)
    for i = 1:length(Î¨.grad_states)
        Base.fill!(Î¨.grad_states[i], v)
    end
end


function -(Î¨::GradVector, Î¦::GradVector)
    res = copy(Î¨)
    LinearAlgebra.axpy!(-1, Î¦.state, res.state)
    for i = 1:length(Î¨.grad_states)
        LinearAlgebra.axpy!(-1, Î¦.grad_states[i], res.grad_states[i])
    end
    return res
end


function *(G::GradgenOperator{num_controls,GT,CGT}, Î±::Number) where {num_controls,GT,CGT}
    GradgenOperator{num_controls,GT,CGT}(G.G * Î±, [CG * Î± for CG in G.control_deriv_ops])
end

*(Î±::Number, G::GradgenOperator) = *(G::GradgenOperator, Î±::Number)


function QuantumPropagators.SpectralRange.random_state(H::GradgenOperator)
    state = QuantumPropagators.SpectralRange.random_state(H.G)
    num_controls = length(H.control_deriv_ops)
    grad_states = [
        QuantumPropagators.SpectralRange.random_state(H.G) for
        i âˆˆ eachindex(H.control_deriv_ops)
    ]
    return GradVector{num_controls,typeof(state)}(state, grad_states)
end


@inline function convert_gradgen_to_dense(G)
    N = size(G.G)[1]
    L = length(G.control_derivs)
    G_full = zeros(eltype(G.G), N * (L + 1), N * (L + 1))
    convert_gradgen_to_dense!(G_full, G)
end


@inline function convert_gradgen_to_dense!(G_full, G)
    N = size(G.G)[1]
    L = length(G.control_derivs)
    @inbounds for i = 1:L+1
        G_full[(i-1)*N+1:i*N, (i-1)*N+1:i*N] .= G.G
    end
    # Set the control-derivatives in the last (block-)column
    @inbounds for i = 1:L
        G_full[(i-1)*N+1:i*N, L*N+1:(L+1)*N] .= G.control_derivs[i]
    end
    return G_full
end


@inline function convert_gradvec_to_dense(Î¨)
    N = length(Î¨.state)
    L = length(Î¨.grad_states)
    Î¨_full = zeros(ComplexF64, N * (L + 1))
    convert_gradvec_to_dense!(Î¨_full, Î¨)
end


@inline function convert_gradvec_to_dense!(Î¨_full, Î¨)
    N = length(Î¨.state)
    L = length(Î¨.grad_states)
    @inbounds for i = 1:L
        Î¨_full[(i-1)*N+1:i*N] .= Î¨.grad_states[i]
    end
    @inbounds Î¨_full[L*N+1:(L+1)*N] .= Î¨.state
    return Î¨_full
end


@inline function convert_dense_to_gradvec!(Î¨, Î¨_full)
    N = length(Î¨.state)
    L = length(Î¨.grad_states)
    @inbounds for i = 1:L
        Î¨.grad_states[i] .= Î¨_full[(i-1)*N+1:i*N]
    end
    @inbounds Î¨.state .= Î¨_full[L*N+1:(L+1)*N]
    return Î¨
end

function Base.convert(::Type{Vector{ComplexF64}}, gradvec::GradVector)
    convert_gradvec_to_dense(gradvec)
end

function Base.convert(::Type{GradVector{num_controls,T}}, vec::T) where {num_controls,T}
    L = num_controls
    N = length(vec) Ã· (L + 1)  # dimension of state
    @assert length(vec) == (L + 1) * N
    grad_states = [vec[(i-1)*N+1:i*N] for i = 1:L]
    state = vec[L*N+1:(L+1)*N]
    return GradVector{num_controls,T}(state, grad_states)
end


function Base.Array{T}(G::GradgenOperator) where {T}
    N, M = size(G.G)
    L = length(G.control_deriv_ops)
    ðŸ˜ = zeros(T, N, M)
    Î¼ = G.control_deriv_ops
    block_rows = [
        hcat([ðŸ˜ for j = 1:i-1]..., Array{T}(G.G), [ðŸ˜ for j = i+1:L]..., Array{T}(Î¼[i]))
        for i = 1:L
    ]
    last_block_row = hcat([ðŸ˜ for j = 1:L]..., Array{T}(G.G))
    return Base.Array{T}(vcat(block_rows..., last_block_row))
end

Base.Array(G::GradgenOperator) = Array{ComplexF64}(G)

function Base.convert(::Type{MT}, G::GradgenOperator) where {MT<:Matrix}
    Base.convert(MT, Base.Array(G))
end

QuantumPropagators._exp_prop_convert_state(::GradVector) = Vector{ComplexF64}
QuantumPropagators._exp_prop_convert_operator(::GradgenOperator) = Matrix{ComplexF64}
QuantumPropagators._exp_prop_convert_operator(::GradGenerator) = Matrix{ComplexF64}
